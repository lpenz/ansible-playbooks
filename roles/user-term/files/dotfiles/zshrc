# lpenz .zshrc

DISABLE_AUTO_TITLE=true


# tramp (https://www.emacswiki.org/emacs/TrampMode#toc6)
if [[ "$TERM" == "dumb" ]]
then
    unsetopt zle
    unsetopt prompt_cr
    unsetopt prompt_subst
    unfunction precmd
    unfunction preexec
    PS1='$ '
    return
fi


source ~/.zgen-github/zgen.zsh
function zgen-config() {
    echo "Creating a zgen save"
    zgen oh-my-zsh
    # plugins
    zgen oh-my-zsh plugins/bgnotify
    zgen oh-my-zsh plugins/z
    zgen oh-my-zsh plugins/colored-man-pages
    zgen oh-my-zsh plugins/go
    zgen load zsh-users/zsh-completions
    zgen load zsh-users/zsh-syntax-highlighting
    zgen load zsh-users/zsh-history-substring-search
    # dircolors
    zgen load joel-porquet/zsh-dircolors-solarized
    # save all to init script
    zgen save
}
if ! zgen saved; then
    zgen-config
fi

zmodload zsh/terminfo
bindkey "$terminfo[kcuu1]" history-substring-search-up
bindkey "$terminfo[kcud1]" history-substring-search-down

setupsolarized dircolors.ansi-dark
PROMPT='%(?,%{$fg[white]%},%{$fg[red]%}) %% '

# Custom configs:
WORDCHARS="*?_-.[]~=/&;!#$%^(){}<>"
setopt no_share_history
setopt clobber

# Hotkeys:
bindkey -M menuselect '^J' accept-and-infer-next-history
bindkey -M menuselect '^H' undo
bindkey -M menuselect '^K' vi-insert
bindkey "\ej" accept-and-menu-complete
bindkey "\el" copy-prev-shell-word
bindkey -s "\e[21~" "\033q\025mymake\012"
bindkey -s "\er" "\033q\025i=0; while ! read -t 1 && ! !!; do i=\$((i+1)); echo \$i; date;  done"

# env config
export VISUAL=emacsclient\ -c
export EDITOR=/usr/bin/vim
export ALTERNATE_EDITOR=/usr/bin/vim
export PAGER=

# aliases
alias vi=emacsclient\ -t

# Load keychain
for f in "$HOME/.ssh/id_rsa" "$HOME/.ssh/id_dsa"; do
    if [ -e "$f" ]; then
        eval `keychain -q --eval --agents ssh $f`
    fi
done

ulimit -v 4000000

# Runs before executing a command
function penz_preexec {
  emulate -L zsh
  setopt extended_glob

  # cmd name only, or if this is sudo or ssh, the next cmd
  local CMD=${1[(wr)^(*=*|sudo|ssh|rake|-*)]:gs/%/%%}
  local LINE="${2:gs/%/%%}"

  title '$CMD' '%100>...>$LINE%<<'
}

# Runs after executing a command, before showing the prompt
function penz_precmd {
  title "%m:%~" "%m:%~"
}

preexec_functions+=(penz_preexec)
precmd_functions+=(penz_precmd)

# do not beep
setterm --blength 0 2>/dev/null 

# Local customizations
if test -r ~/.zshrc.local; then . ~/.zshrc.local; fi
