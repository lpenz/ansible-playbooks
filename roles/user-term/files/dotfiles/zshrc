# -*- shell-script -*-

# lpenz .zshrc

DISABLE_AUTO_TITLE=true

# tramp (https://www.emacswiki.org/emacs/TrampMode#toc6)
if [[ "$TERM" == "dumb" ]]
then
    unsetopt zle
    unsetopt prompt_cr
    unsetopt prompt_subst
    unfunction precmd 2>/dev/null
    unfunction preexec 2>/dev/null
    PS1='$ '
    return
fi

# Local zsh, namespace trampoline
if test -r ~/.zshrc.local.pre; then . ~/.zshrc.local.pre; fi

if ! [ -d "$HOME/.zplugin/bin" ]; then
   mkdir -p "$HOME/.zplugin"
   rm -rf "$HOME/.zplugin/bin"
   git clone https://github.com/zdharma/zplugin.git "$HOME/.zplugin/bin"
fi

function lpenz_powerlevel9k_setup {
    if [ "$TERM" = "rxvt-256color" ]; then
        export LC_POWERLINE_FONTS=true
    fi

    if [ "$LC_POWERLINE_FONTS" = "true" ]; then
        POWERLEVEL9K_MODE='nerdfont-complete'
    fi

    if [[ "$UID" -eq 0 ]]; then
        prompt_user_indicator() {
            "$1_prompt_segment" "$0" "$2" "$DEFAULT_COLOR" yellow '' 0 '' '#'
        }
    else
        prompt_user_indicator() {
            "$1_prompt_segment" "$0" "$2" "$DEFAULT_COLOR" blue '' 0 '' '%%'
        }
    fi
    if [[ "$UID" -eq 0 || "$USER" = "lpenz" ]]; then
        POWERLEVEL9K_CONTEXT_TEMPLATE=%m
    fi

    GITSTATUS_DAEMON_CANDIDATE="$HOME/.zplugin/plugins/romkatv---powerlevel10k/gitstatus/bin/gitstatusd-linux-x86_64-static"
    if "$GITSTATUS_DAEMON_CANDIDATE" -h &>/dev/null; then
        GITSTATUS_DAEMON="$GITSTATUS_DAEMON_CANDIDATE"
    fi
    POWERLEVEL9K_LEFT_PROMPT_ELEMENTS=(user_indicator)
    POWERLEVEL9K_RIGHT_PROMPT_ELEMENTS=(background_jobs vcs context dir)
}

function lpenz_completion_setup {
    WORDCHARS="*?_-.[]~=/&;!#$%^(){}<>"
    bindkey -M menuselect '^J' accept-and-infer-next-history
    bindkey -M menuselect '^H' undo
    bindkey -M menuselect '^K' vi-insert
}

# plugin management

# zplugin {
source ~/.zplugin/bin/zplugin.zsh
zplugin ice lucid
zplugin snippet OMZ::lib/termsupport.zsh
zplugin ice lucid wait'!0'
zplugin snippet OMZ::lib/completion.zsh
zplugin ice lucid wait'0'
zplugin snippet OMZ::lib/key-bindings.zsh
zplugin ice lucid wait'0'
zplugin snippet OMZ::plugins/colored-man-pages/colored-man-pages.plugin.zsh
zplugin ice lucid wait'0'
zplugin load zsh-users/zsh-completions
zplugin ice lucid
zplugin snippet OMZ::lib/history.zsh
zplugin ice lucid wait'!0'
zplugin snippet OMZ::plugins/history/history.plugin.zsh
zplugin ice lucid wait'0'
zplugin load zsh-users/zsh-history-substring-search
zplugin ice wait'!0' lucid atload'setupsolarized dircolors.ansi-dark'
zplugin load 'joel-porquet/zsh-dircolors-solarized'
zplugin ice lucid wait'0'
zplugin load 'lpenz/zsh-notify-urxvt'
zplugin ice lucid src'powerlevel10k.zsh-theme' atinit'lpenz_powerlevel9k_setup'
zplugin load 'romkatv/powerlevel10k'
zplugin ice lucid from"gh-r" as"program"
zplugin load junegunn/fzf-bin
zplugin ice lucid wait'0' from"gh" 
zplugin snippet https://github.com/junegunn/fzf/blob/master/shell/completion.zsh
zplugin ice lucid wait'0' from"gh" 
zplugin snippet https://github.com/junegunn/fzf/blob/master/shell/key-bindings.zsh
zplugin ice lucid wait'!2' atinit'zpcompinit; zpcdreplay' atload'lpenz_completion_setup'
zplugin load 'zdharma/fast-syntax-highlighting'
# } zplugin

zmodload zsh/terminfo
bindkey "$terminfo[kcuu1]" history-substring-search-up
bindkey "$terminfo[kcud1]" history-substring-search-down

# Custom configs:
setopt no_share_history
setopt clobber

# Hotkeys:
bindkey "\ej" accept-and-menu-complete
bindkey -s "\e[21~" "\033q\025mymake\012"
bindkey -s "\er" "\033q\025i=0; while ! read -t 1 && ! !!; do i=\$((i+1)); echo \"======== \$i \$(date)\";  done"
autoload -U copy-earlier-word
zle -N copy-earlier-word
bindkey "\e;" copy-earlier-word
bindkey -r "\el"

# env config
export VISUAL=vim
export EDITOR=vim
export ALTERNATE_EDITOR=/usr/bin/vim
export PAGER=cat

# aliases
alias rxvt-font-set="printf '\e]710;%s\007' "
alias ssh-nohostkey="ssh -o StrictHostKeyChecking=no -o UserKnownHostsFile=/dev/null"
alias ls="ls --color=tty"
alias ll="ls -l"
alias grep='grep  --color=auto --exclude-dir={.bzr,CVS,.git,.hg,.svn}'
alias gtags='gtags-lpenz-wrapper'

# Load keychain
if which keychain &> /dev/null; then
   for f in "$HOME/.ssh/id_rsa" "$HOME/.ssh/id_dsa"; do
      if [ -e "$f" ]; then
         eval `keychain -q --eval --agents ssh $f`
      fi
   done
fi

ulimit -v 4000000

# Runs before executing a command
function lpenz_preexec {
    emulate -L zsh
    setopt extended_glob

    # cmd name only, or if this is sudo or ssh, the next cmd
    local CMD=${1[(wr)^(*=*|sudo|ssh|rake|-*)]:gs/%/%%}
    local LINE="${2:gs/%/%%}"

    title '$CMD' '%100>...>$LINE%<<'
    lpenz_lastcmd=$1
}

function humanReadableDurationRender {
    local humanReadableDuration
    if (( _P9K_COMMAND_DURATION > 3600 )); then
        humanReadableDuration=$(TZ=GMT; strftime '%H:%M:%S' $(( int(rint(_P9K_COMMAND_DURATION)) )))
    elif (( _P9K_COMMAND_DURATION > 60 )); then
        humanReadableDuration=$(TZ=GMT; strftime '%M:%S' $(( int(rint(_P9K_COMMAND_DURATION)) )))
    else
        typeset -F 2 humanReadableDuration
        humanReadableDuration=$_P9K_COMMAND_DURATION
    fi
    echo "$humanReadableDuration"
}

# Runs after executing a command, before showing the prompt
function lpenz_lastcmdstatus {
    if [ "$_P9K_EXIT_CODE" != 0 ] || (( _P9K_COMMAND_DURATION > 3 )); then
        if [ "$_P9K_EXIT_CODE" != 0 ]; then
            local color=red
        else
            local color=green
        fi
        print -nP "%K{$color}\e[38;5;16m\ue0b8%f %f%K{$color}"
        if [ "$_P9K_EXIT_CODE" != 0 ]; then
            print_icon FAIL_ICON
            print -nP " $_P9K_EXIT_CODE"
        else
            print_icon OK_ICON
        fi
        if (( _P9K_COMMAND_DURATION > 3 )); then
            local humanReadableDuration=$(humanReadableDurationRender $_P9K_COMMAND_DURATION)
            print -nP "  ${icons[EXECUTION_TIME_ICON]} $humanReadableDuration"
            print -nP "  \u231a $(strftime '%F %T' $EPOCHSECONDS)"
        fi
        print -P " %K{$color}\e[38;5;16m\ue0ba\e[0m \n"
    fi
}

function lpenz_precmd {
    title "%m:%~" "%m:%~"
    lpenz_lastcmdstatus
}

preexec_functions+=(lpenz_preexec)
precmd_functions+=(lpenz_precmd)

# do not beep
setterm --blength 0 2>/dev/null 

# Local customizations
if test -r ~/.zshrc.local; then . ~/.zshrc.local; fi

# keep this comment here to avoid auto-insertion:
# [ -f ~/.fzf.zsh ] && source ~/.fzf.zsh
